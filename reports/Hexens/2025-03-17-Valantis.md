**Auditors**

[Hexens](https://hexens.io/)

---

# Findings
## High Risk

### [VLTS3-5] The calculation of shares in STEXAMM is incorrect because of using the outdated amountToken1PendingLPWithdrawal

**Severity:** Critical

**Path:** src/STEXAMM.sol#L468-L479, src/STEXAMM.sol#L550-L555

**Description:** The calculation of shares in `deposit()` function of STEXAMM contract:
```
(uint256 reserve0Pool, uint256 reserve1Pool) = ISovereignPool(pool).getReserves();
// Account for token0 in pool (liquid) and pending unstaking (locked)
uint256 reserve0Total = reserve0Pool + _withdrawalModule.amountToken0PendingUnstaking();
// Account for token1 pending withdrawal to LPs (locked)
uint256 reserve1PendingWithdrawal = _withdrawalModule.amountToken1PendingLPWithdrawal();
// shares calculated in terms of token1
shares = Math.mulDiv(
    _amount,
    totalSupplyCache,
    reserve1Pool + _withdrawalModule.amountToken1LendingPool()
        + _withdrawalModule.convertToToken1(reserve0Total) - reserve1PendingWithdrawal
);
```
`amountToken0PendingUnstaking()` is the amount that the withdrawal module is waiting for the overseer contract to claim after `unstakeToken0Reserves`. In `stHYPEWithdrawalModule`, this getter function is always up to date:
```
function amountToken0PendingUnstaking() public view override returns (uint256) {
    uint256 balanceNative = address(this).balance;
    uint256 excessNative =
        balanceNative > amountToken1ClaimableLPWithdrawal ? balanceNative - amountToken1ClaimableLPWithdrawal : 0;
    // stHYPE is rebase, hence no need for conversion
    uint256 excessToken0 = excessNative > 0 ? excessNative : 0;

    uint256 amountToken0PendingUnstakingCache = _amountToken0PendingUnstaking;
    if (amountToken0PendingUnstakingCache > excessToken0) {
        return amountToken0PendingUnstakingCache - excessToken0;
    } else {
        return 0;
    }
}
```
However, `amountToken1PendingLPWithdrawal` is not the same, since it doesn’t have a getter function and is not updated with the current native balance in the `stHYPEWithdrawalModule` contract. Therefore, when the overseer has already sent native Token1 from unstaking but `update()` hasn’t been called, `amountToken1PendingLPWithdrawal` remains outdated, leading to a miscalculation of the total assets in the share calculation.

For example, after some withdrawals are queued, `amountToken1PendingLPWithdrawal` increases to 10e18 from `burnToken0AfterWithdraw`. Then, the owner calls `stHYPEWithdrawalModule::unstakeToken0Reserves `for the first time with `amount = 10e18` to unstake Token0 from the Sovereign pool and burn it via the Overseer contract to claim native Token1 asynchronously.

Before the overseer sends tokens:
`amountToken1PendingLPWithdrawal = 10e18`, `amountToken0PendingUnstaking = 10e18`
=> Total assets = `reserve0 + reserve1 + amountToken1LendingPool + amountToken0PendingUnstaking - amountToken1PendingLPWithdrawal
= reserve0 + reserve1 + amountToken1LendingPool + 10e18 - 10e18`

After the overseer sends 4e18 native tokens, but `update()` hasn’t been called:
`amountToken1PendingLPWithdrawal = 10e18` (still outdated), `amountToken0PendingUnstaking = 4e18 `(up-to-date via getter function)
=> Total assets = `reserve0 + reserve1 + amountToken1LendingPool + 4e18 - 10e18`

Therefore, the user’s shares will be calculated incorrectly due to the use of the outdated `amountToken1PendingLPWithdrawal`, leading to a loss of shares when depositing. 

The same issue exists in the calculation of the `withdraw` function.

**Remediation:**  You should trigger update() whenever a deposit or withdrawal occurs, or create a getter function for amountToken1PendingLPWithdrawal to keep it updated at all times.

**Status:**  Fixed


- - -

### [VLTS3-13] Direct theft of surplus balance when unstaking stHYPE

**Severity:** Critical

**Path:** src/stHYPEWithdrawalModule.sol:update#L431-L475

**Description:** The stHYPE Withdrawal Module exposes functionality for the owner of the module to unstake stHYPE into HYPE using the Overseer contract. The owner calls `unstakeToken0Reserves`, which in turn transfers stHYPE from the SovereignPool (uses the STEXAMM) and then calls `burnAndRedeemIfPossible` to create a Burn request. The stHYPE is burned immediately and the Burn request can be redeemed later once filled for HYPE.

In the `update` function, any received HYPE from redeemed Burn requests is first allocated to any outstanding LP withdrawal requests after which the surplus is deposited back into the Sovereign Pool directly on lines 471-474:
```
token1.deposit{value: balanceSurplus}();
// Pool reserves are measured as balances, hence we can replenish it with token1
// by transfering directly
token1.safeTransfer(stexInterface.pool(), balanceSurplus);
```
This works because the token is a rebase token and so the Sovereign Pool uses the actual balance as reserve.

However, this mechanism can be exploited to directly steal the entire surplus amount by leveraging the swap callback of the Sovereign Pool.

When calling `swap` on the Sovereign Pool, the user has the option parameter `isSwapCallback`, which can be turned on to pay the `tokenIn` amount inside of the callback in `_handleTokenInTransfersOnSwap`:
```
    function _handleTokenInTransfersOnSwap(
        bool isZeroToOne,
        bool isSwapCallback,
        IERC20 token,
        uint256 amountInUsed,
        uint256 effectiveFee,
        bytes calldata _swapCallbackContext
    ) private {
        uint256 preBalance = token.balanceOf(sovereignVault);

        if (isSwapCallback) {
            ISovereignPoolSwapCallback(msg.sender).sovereignPoolSwapCallback(
                address(token),
                amountInUsed,
                _swapCallbackContext
            );
        } else {
            token.safeTransferFrom(msg.sender, sovereignVault, amountInUsed);
        }

        uint256 amountInReceived = token.balanceOf(sovereignVault) - preBalance;
        
        [..]
    }
```
Any balance transferred to the Sovereign Pool during this callback would be counted towards payment for the user. As such, it is possible to call `stHYPEWithdrawalModule.update` inside of the callback, where the surplus amount in HYPE is transferred directly.

As a result, the entire amount can be used by the attacker to swap to stHYPE, without actual paying and thus directly stealing the value from the protocol. 
```
    function update() external nonReentrant {
        // Need to ensure that enough native token is reserved for settled LP withdrawals
        // WARNING: This implementation assumes that there is no slashing enabled in the LST protocol
        uint256 amountToken1ClaimableLPWithdrawalCache = amountToken1ClaimableLPWithdrawal;
        if (address(this).balance <= amountToken1ClaimableLPWithdrawalCache) {
            return;
        }

        // Having a surplus balance of native token means that new unstaking requests have been fulfilled
        uint256 balanceSurplus = address(this).balance - amountToken1ClaimableLPWithdrawalCache;
        // stHYPE is rebase, hence no need for conversion
        uint256 balanceSurplusToken0 = balanceSurplus;

        uint256 amountToken0PendingUnstakingCache = _amountToken0PendingUnstaking;
        if (amountToken0PendingUnstakingCache > balanceSurplusToken0) {
            _amountToken0PendingUnstaking = amountToken0PendingUnstakingCache - balanceSurplusToken0;
        } else {
            _amountToken0PendingUnstaking = 0;
        }

        // Prioritize LP withdrawal requests
        uint256 amountToken1PendingLPWithdrawalCache = amountToken1PendingLPWithdrawal;
        if (balanceSurplus > amountToken1PendingLPWithdrawalCache) {
            balanceSurplus -= amountToken1PendingLPWithdrawalCache;
            amountToken1ClaimableLPWithdrawal += amountToken1PendingLPWithdrawalCache;
            cumulativeAmountToken1ClaimableLPWithdrawal += amountToken1PendingLPWithdrawalCache;
            amountToken1PendingLPWithdrawal = 0;
        } else {
            amountToken1PendingLPWithdrawal -= balanceSurplus;
            amountToken1ClaimableLPWithdrawal += balanceSurplus;
            cumulativeAmountToken1ClaimableLPWithdrawal += balanceSurplus;
            balanceSurplus = 0;
            return;
        }

        // Wrap native token into token1 and re-deposit into the pool
        ISTEXAMM stexInterface = ISTEXAMM(stex);
        address token1Address = stexInterface.token1();
        IWETH9 token1 = IWETH9(token1Address);

        token1.deposit{value: balanceSurplus}();
        // Pool reserves are measured as balances, hence we can replenish it with token1
        // by transfering directly
        token1.safeTransfer(stexInterface.pool(), balanceSurplus);
    }
```

**Remediation:**  It is not safe to make direct transfers to the pool if you are unsure whether current execution is inside of a swap callback. It should therefore be verified using the reentrancy switch of the Sovereign Pool, however this is currently not exposed.

One possibility is to add a `donate` function to the SovereignPool that only works for rebase tokens (donations are possible with transfers already anyway) and has the `nonReentrant` modifier. That way you can safely donate the tokens back to the pool without the risk.

If modifications to the SovereignPool are not possible, then dummy calls that check whether reverts happen due to reentrancy are needed. This is often used to guard against read-only reentrancy for Balancer or Curve.

**Status:**  Fixed

- - -

### [VLTS3-3] Loss of funds when setting a new lendingModule with stHYPEWithdrawalModule::setProposedLendingModule

**Severity:** High

**Description:** When a new `lendingModule` is set via `stHYPEWithdrawalModule::setProposedLendingModule` the wrapped native token is transferred from the current `lendingModule` to the `stHYPEWithdrawalModule` contract:
```
if (address(lendingModule) != address(0)) {
    lendingModule.withdraw(lendingModule.assetBalance(), address(this));
}
```
As a consequence, these funds may be stuck in the `stHYPEWithdrawalModule` contract since they are not transferred to the new lending module (`lendingModuleProposal.lendingModule`) and there is also no other way to transfer the withdrawn tokens.
```
function setProposedLendingModule() external onlyOwner {
    if (lendingModuleProposal.startTimestamp > block.timestamp) {
        revert stHYPEWithdrawalModule__setProposedLendingModule_ProposalNotActive();
    }

    if (lendingModuleProposal.startTimestamp == 0) {
        revert stHYPEWithdrawalModule__setProposedLendingModule_InactiveProposal();
    }

    if (address(lendingModule) != address(0)) {
        lendingModule.withdraw(lendingModule.assetBalance(), address(this));
    }

    lendingModule = ILendingModule(lendingModuleProposal.lendingModule);
    delete lendingModuleProposal;
    emit LendingModuleSet(address(lendingModule));
}
```


**Remediation:**  We recommend to set the withdraw receiver as the SovereignPool, such that the withdrawn tokens would simply be counted as reserve. Afterwards, the owner can re-deposit any amount of assets back into the new lending module using `supplyToken1ToLendingPool`.

**Status:**  Fixed


- - -

### [VLTS3-9] Withdrawal queue priority bypass for feeless instant withdrawal

**Severity:** High

**Description:** When a `Withdrawal` is created, it references the `cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint` pointer to track when a claim is allowed, according to the existing withdrawal queue.
```
  LPWithdrawals[idLPWithdrawal] = LPWithdrawalRequest({
      recipient: _recipient,
      amountToken1: amountToken1.toUint96(),
      cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint: cumulativeAmountToken1ClaimableLPWithdrawal
  });
```
However, `cumulativeAmountToken1ClaimableLPWithdrawal` only gets updated after a call to the `update` function and only if the HYPE was received from the Overseer. So any withdrawal request made after another one while it's not claimable yet are in the same queue at the same cumulative amount:
```
if (address(this).balance <= amountToken1ClaimableLPWithdrawalCache) {
        amountToken1ClaimableLPWithdrawal = address(this).balance;
        return;
}
```
Moreover, it does not keep withdrawal sizes in mind. If a user withdraws 100 ETH, and the `cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint` is X, they can claim the withdrawal at X + 100.

The following logic allows smaller deposits to be bypass the priority queue and effectively gain feeless instant withdrawals of their stHYPE to HYPE.
```
if (
    cumulativeAmountToken1ClaimableLPWithdrawal
        < request.cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint + request.amountToken1
) {
    revert stHYPEWithdrawalModule__claim_cannotYetClaim();
}
```
Consider the following situation:

1. User 1 withdraws 10 ETH (`cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint = X`), so they can withdraw at  `X + 10`.

2. The burn request is filled and redeemed, the Overseer sends 10 ETH to the Withdrawal Module.

3. User 2 withdraws 1 ETH.

4. Update is now called and fills 10 ETH.

5. User 2 can claim.

6. User 1 can claim but reverts due to not enough funds (only 9 ETH available).
```
    function burnToken0AfterWithdraw(uint256 _amountToken0, address _recipient)
        external
        override
        onlySTEX
        nonReentrant
    {
        // stHYPE is rebase, hence to need for conversion
        uint256 amountToken1 = _amountToken0;

        amountToken1PendingLPWithdrawal += amountToken1;

        emit LPWithdrawalRequestCreated(idLPWithdrawal, amountToken1, _recipient);

        LPWithdrawals[idLPWithdrawal] = LPWithdrawalRequest({
            recipient: _recipient,
            amountToken1: amountToken1.toUint96(),
            cumulativeAmountToken1ClaimableLPWithdrawalCheckpoint: cumulativeAmountToken1ClaimableLPWithdrawal
        });
        idLPWithdrawal++;
    }
```

**Remediation:**  The creation of the LP withdrawal request should only take the order into account, e.g. by keeping a cumulative withdrawn amount that can then be used in `claim` to ensure that the claimer has the rights to the claimable ETH.

**Status:**   Fixed


- - -
## Medium Risk

### [VLTS3-14] Read-only reentrancy in withdraw can lead to swap fee manipulation

**Severity:** Medium

**Path:** src/STEXAMM.sol:withdraw#L523-L624

**Description:** In the `withdraw` function for LP shareholders in STEXAMM, the caller can choose to unwrap their wHYPE and receive native HYPE instead. This happens on lines 599-602 and an external call is made to the receiver, giving them execution.

Even though the function itself has the `nonReentrant` modifier, it is still an intermediate state: the `token0` (stHYPE) has already been burned previously using `burnToken0AfterWithdraw` on line 589, while remaining `token1` (wHYPE) is yet to be taken out of the pool using `withdrawLiquidity` on line 611.

Even though the pool itself does not use the `token0/token1` ratio for valuation, it does use this ratio to calculate the swap fee in STEXRatioSwapFeeModule:getSwapFeeInBips, which is used in SovereignPool:swap.

As such, a user can perform a swap inside of the receive callback, when the ratio of `token0/token1` is significantly lower than normal, and get less swap fees as a result.

The reentrancy guard of `STEXAMM` won’t be triggered, because `getLiquidityQuote` is `view` and not marked as `nonReentrant`.
```
    function withdraw(
        uint256 _shares,
        uint256 _amount0Min,
        uint256 _amount1Min,
        uint256 _deadline,
        address _recipient,
        bool _unwrapToNativeToken,
        bool _isInstantWithdrawal
    ) external override nonReentrant returns (uint256 amount0, uint256 amount1) {
        [..]
        
        if (cache.amount1LendingPool > 0) {
          _withdrawalModule.withdrawToken1FromLendingPool(
              cache.amount1LendingPool, _unwrapToNativeToken ? address(this) : _recipient
          );

          if (_unwrapToNativeToken) {
              IWETH9(token1).withdraw(cache.amount1LendingPool);
              Address.sendValue(payable(_recipient), cache.amount1LendingPool);
          }
        }
        
        [..]
    }
```

**Remediation:**  It is advisable to use a modifier for `view` function that are susceptible to read-only reentrancy. The modifier only checks whether the reentrancy guard is active and does not need to set it.

**Status:** Fixed

- - -
## Low Risk

### [VLTS3-7] Potential DoS condition when trying to set a new proposed lending module

**Severity:** Low

**Path:** src/stHYPEWithdrawalModule.sol#L312

**Description:** [AAVE V3 Pool](https://github.com/aave/aave-v3-core/blob/782f51917056a53a2c228701058a6c3fb233684a/contracts/protocol/libraries/logic/ValidationLogic.sol#L66-L71) reverts when withdrawing zero amount:
```
function validateWithdraw(
    DataTypes.ReserveCache memory reserveCache,
    uint256 amount,
    uint256 userBalance
  ) internal view {
    require(amount != 0, Errors.INVALID_AMOUNT);
```
Thus, when updating a lending module (`AaveLendingModule.sol`) with `assetBalance() = 0` via `stHYPEWithdrawalModule::setProposedLendingModule` it will revert:
```
if (address(lendingModule) != address(0)) {
    lendingModule.withdraw(lendingModule.assetBalance(), address(this));
}
```
```
function setProposedLendingModule() external onlyOwner {
    if (lendingModuleProposal.startTimestamp > block.timestamp) {
        revert stHYPEWithdrawalModule__setProposedLendingModule_ProposalNotActive();
    }

    if (lendingModuleProposal.startTimestamp == 0) {
        revert stHYPEWithdrawalModule__setProposedLendingModule_InactiveProposal();
    }

    if (address(lendingModule) != address(0)) {
        lendingModule.withdraw(lendingModule.assetBalance(), address(this));
    }

    lendingModule = ILendingModule(lendingModuleProposal.lendingModule);
    delete lendingModuleProposal;
    emit LendingModuleSet(address(lendingModule));
}
```

**Remediation:**  Consider checking the `assetBalance()` to not being 0 before calling `lendingModule.withdraw()` inside `stHYPEWithdrawalModule::setProposedLendingModule`.

For example:
```
if (address(lendingModule) != address(0) && lendingModule.assetBalance() != 0) {
    lendingModule.withdraw(lendingModule.assetBalance(), address(this));
}
```

**Status:** Fixed


- - -

### [VLTS3-10] Owner is able to set new FeeParams without a time lock

**Severity:** Low

**Path:** src/STEXRatioSwapFeeModule.sol:setSwapFeeParams#L155-L192

**Description:** The `SwapFeeModule` in `STEXAMM` is set through a proposal of a new `STEXRatioSwapFeeModule` by calling the function `setProposedSwapFeeModule`, however the new fee parameters can be set anytime after the proposal was passed without a time lock.

Typically, a new fee module with the updated fee becomes active after 7 days. However, calling `setSwapFeeParams` allows the owner to change the `STEXRatioSwapFeeModule` at any time, without needing to propose and wait for any time to pass when calling `proposeSwapFeeModule` on the `STEXAMM` contract.

There are some safeguards in the `setSwapFeeParams` function, but it could still allow the owner to set the fee to 50% instantly.
```
    function setSwapFeeParams(
        uint32 _minThresholdRatioBips,
        uint32 _maxThresholdRatioBips,
        uint32 _feeMinBips,
        uint32 _feeMaxBips
    ) external override onlyOwner {
        // Reserve ratio threshold params must be in BIPS
        if (_minThresholdRatioBips >= BIPS) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_invalidMinThresholdRatio();
        }
        if (_maxThresholdRatioBips > BIPS) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_invalidMaxThresholdRatio();
        }
        if (_minThresholdRatioBips >= _maxThresholdRatioBips) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_inconsistentThresholdRatioParams();
        }

        // Fees must be lower than 50% (5_000 bips)
        if (_feeMinBips >= BIPS / 2) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_invalidFeeMin();
        }
        if (_feeMaxBips >= BIPS / 2) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_invalidFeeMax();
        }

        if (_feeMinBips > _feeMaxBips) {
            revert STEXRatioSwapFeeModule__setSwapFeeParams_inconsistentFeeParams();
        }

        feeParams = FeeParams({
            minThresholdRatioBips: _minThresholdRatioBips,
            maxThresholdRatioBips: _maxThresholdRatioBips,
            feeMinBips: _feeMinBips,
            feeMaxBips: _feeMaxBips
        });

        emit SwapFeeParamsSet(_minThresholdRatioBips, _maxThresholdRatioBips, _feeMinBips, _feeMaxBips);
    }
```

**Remediation:**  We would recommend to implement some time lock for the setting of fee parameters as well, to allow for withdrawals to happen, as the fee parameters also affect LPs that do instant withdrawals and not just users that perform swaps.

**Status:**  Acknowledged

- - -

### [VLTS3-11] Aave lending module could be a source of temporary DoS

**Severity:** Low

**Path:** src/AaveLendingModule:withdraw#L75-L77

**Description:** The STEXAMM leverages supplying token1 (WETH) collateral to a lending platform (Aave) to earn extra yield. The depositing happens manually by the owner, but the withdrawing happens automatically upon a call to `STEXAMM.sol:withdraw` to fulfil the callers withdrawal.

The code assumes that the withdraw function of Aave can always return the provided collateral but this is not the case if the liquidity is being borrowed on Aave. Aave often utilises a utilisation factor of 90%, which means that only 10% of the supplied liquidity can be withdrawn at the time. If a higher amount is requested, it would simply revert.

Since HyperEVM is a new chain, we can assume that the STEXAMM will be a major supplied of wHYPE to Aave on HyperEVM and potentially hold more than 10% of the supplied liquidity. In that case, larger withdrawals could result in funds not being withdrawable and temporary insolvency/DoS.
```
    function withdraw(uint256 _amount, address _recipient) external onlyOwner {
        pool.withdraw(asset, _amount, _recipient);
    }
```

**Remediation:**  The function `supplyToken1ToLendingPool` allows the owner to withdraw all liquidity from the SovereignPool and supply it to Aave, we recommend that this function should be capped by a sensible percentage of the total reserves to reduce the chance of Aave blocking withdrawals.

**Status:** Acknowledged

- - -