**Auditors**

[Hexens](https://hexens.io/)

---

# Findings
## High Risk

### [ONCH-6] Infinite voting power

**Severity:** Critical

**Path:** ERC20Pods.sol:_updateBalances#L115-L132

**Description:** 

The 1INCH staking contract `St1inch.sol` implements `ERC20Pods.sol` and therefore supports attaching pods for delegating voting power, such as the pod `PowerPod.sol`. A user can add a pod for themselves by calling `ERC20Pods.sol:addPod`. The pod will receive an external call upon every token transfer to correspondingly update their balances, as implemented in `ERC20Pods.sol:_afterTokenTransfer` and `Pod.sol:updateBalances`.

This external call in `ERC20Pods.sol:_updateBalances` is limited in gas and implemented using an external call in Yul (assembly). Because of the assembly call, the return code (i.e. whether the external was successful or not) is ignored. This ultimately means that any state-changing effects that happened before or will happen after the external call will still go through.

Another important aspect is that `PowerPod.sol` requires the user to call `PowerPod.sol:register`, which creates a `DelegatedShare.sol` contract. This contract also implements `ERC20Pods.sol` and further allows adding delegation pods. The `ERC20Pods.sol:addPod` function allows any address to be added as a pod, including attacker-controlled contracts, but only to oneself. 

The `St1inch.sol` contract has a limit of 500K gas for the external call to the `PowerPod.sol` and the `DelegatedShare.sol` has a limit of 150K gas for each registered pod. Furthermore, the `DelegatedShare.sol` has a maximum of 3 pods per user. This means that a user can register 3 pods that burn all 150K gas and leave 50K gas for all of the operations in `PowerPod.sol`. This is insufficient and it is therefore possible to force a revert of the external call from `St1inch.sol` to `PowerPod.sol`.

This vulnerability can be exploited to generate infinite voting power on a `PowerPod.sol` and `DelegatedShare.sol` contract. Basically, when a `St1inch.sol` balance update happens, the attached `PowerPod.sol`  and its `DelegatedShare.sol` should also update the user's balance correspondingly. However, the external call can be forcefully reverted and the result code is ignored. As a result, the balance in `St1inch.sol` is updated, but the balance in `PowerPod.sol` is not.

The following scenario shows a more practical example:

1. A user has 1 1INCH.

2. The user calls `PowerPod.sol:register`, which creates a `DelegatedShare.sol` for the user.

3. The user calls `PowerPod.sol:delegate` and delegates to themselves.

4. The user calls `St1inch.sol:addPod` with the address of the RDP pod.

5. The user calls `St1inch.sol:deposit` to deposit their 1 1INCH and receives 1 ST1INCH (1:1 for simplicity’s sake).

6. The deposit triggered `St1inch.sol:_afterTokenTransfer`, which called `updateBalances` on the RDP. At this moment, the user has a balance of 1 ST1INCH, 1 RDP and 1 DS.

7. Now the user calls `DelegatedShare.sol:addPod` with a random address, e.g. `address(1)`. This will cause the `DelegatedShare.sol:_afterTokenTransfer` and consequently the `PowerPod.sol:updatesBalances` to always revert.

8. Afterwards the user calls `St1inch.sol:withdraw` for 1 1INCH (instant withdrawals for simplicity’s sake).

9. The withdrawal triggered `St1inch.sol:_afterTokenTransfer`, which calls the always reverting `updateBalances` on the RDP, but the transaction still succeeds. At this moment, the user has a balance of 0 ST1INCH, 1 RDP and 1 DS.

10. The user calls `DelegatedShare.sol:removePod` with the same address as step 7.

11. The user calls `St1inch.sol:deposit` again for 1 1INCH.

12. Now the user has a balance of 1 ST1INCH, 2 RDP and 2 DS.

13. Repeat step 7-12 for infinite voting power.

```
function _updateBalances(address pod, address from, address to, uint256 amount) private {
    bytes4 selector = IPod.updateBalances.selector;
    bytes4 exception = InsufficientGas.selector;
    uint256 gasLimit = podCallGasLimit;
    assembly {  // solhint-disable-line no-inline-assembly
        let ptr := mload(0x40)
        mstore(ptr, selector)
        mstore(add(ptr, 0x04), from)
        mstore(add(ptr, 0x24), to)
        mstore(add(ptr, 0x44), amount)

        if lt(div(mul(gas(), 63), 64), gasLimit) {
            mstore(0, exception)
            revert(0, 4)
        }
        pop(call(gasLimit, pod, 0, ptr, 0x64, 0, 0))
    }
}
```

**Remediation:**  We would recommend to increase the gas limit in St1inch.sol (e.g. from 500K to 530K) or to decrease the gas limit in DelegatedShare.sol (e.g. from 150K to 140K). This would leave enough gas to execute the external call.

**Status:**  Fixed



- - -

## Low Risk

### [ONCH-6] Arithmetic gas optimisation

**Severity:** Low

**Path:** St1nch.sol:_withdraw#L202

**Description:** 

In the function `St1nch.sol:_withdraw` on line 202 the unlock time of the deposit is set to the minimum of the original unlock time and the current timestamp. However, this arithmetic operation is redundant and the unlock time of the deposit should be set directly to the current timestamp.

If it is an early withdrawal or a withdrawal at exactly the unlock time, then the unlock time will be set to the current timestamp and in the next deposit in the function `St1nch.sol:_deposit` on line 129 the maximum operation will return the current timestamp.

If it is a late withdrawal, then the unlock time will be set to the original unlock time, but in the next deposit the maximum on line 129 will always be the current timestamp.

In all cases will the calculated unlock time on line 129 return the current timestamp.

```
function _withdraw(Depositor memory depositor, uint256 amount, uint256 balance) private {
    totalDeposits -= amount;
    depositor.amount = 0;
    // keep unlockTime in storage for next tx optimization
    depositor.unlockTime = uint40(Math.min(depositor.unlockTime, block.timestamp));
    depositors[msg.sender] = depositor; // SSTORE
    _burn(msg.sender, balance);
}
```

**Remediation:**  In `St1nch.sol:_withdraw` on line 202 `depositor.unlockTime` should be set directly to `block.timestamp` as this will save gas and does not change the lock/unlock time logic.

**Status:**  Fixed

- - -

### [ONCH-4]

**Severity:** Low

**Path:** St1inch.sol:setFeeReceiver#L70-L73, FeeBank.sol:_depositFor#L104-L108

**Description:** 

1. In `St1inch.sol:setFeeReceiver` the parameter `feeReceiver_` is not checked against the zero address. If this address is set to `address(0)`, then the early withdrawal function would always revert.

2. In `FeeBank.sol:_depositFor` the parameter `account` is not checked against the zero address. This zero address could be propagated from `depositFor` and it would increase the balance of `address(0)`.

```function setFeeReceiver(address feeReceiver_) external onlyOwner {
    feeReceiver = feeReceiver_;
    emit FeeReceiverSet(feeReceiver_);
}
```
```
function _depositFor(address account, uint256 amount) internal returns (uint256 totalAvailableCredit) {
    _token.safeTransferFrom(msg.sender, address(this), amount);
    _accountDeposits[account] += amount;
    totalAvailableCredit = _charger.increaseAvailableCredit(account, amount);
}
```

**Remediation:**  Add a check against the zero address in `St1inch.sol:setFeeReceiver` for the parameter `feeReceiver_` and in `FeeBank.sol:_depositFor` for the parameter `account`.

**Status:** Fixed


- - -

### [ONCH-5] Early withdrawal is impossible if the Fee Receiver is not set

**Severity:** Low

**Path:** St1inch.sol:earlyWithdrawTo#L155-L170

**Description:** 

The function `St1inch.sol:earlyWithdrawTo` is designed to give users the opportunity to withdraw earlier than the staking period end time. However, in the case where the `feeReceiver` address has not yet been set, then there will be no way to make an early withdrawal as the `feeReceiver` address will be zero address, and the `transfer` function call will revert.

This will be the case when the contract has been created as the `feeReceiver` is not a parameter in the constructor.

```
function earlyWithdrawTo(address to, uint256 minReturn, uint256 maxLoss) public {
    Depositor memory depositor = depositors[msg.sender]; // SLOAD
    if (emergencyExit || block.timestamp >= depositor.unlockTime) revert StakeUnlocked();
    uint256 allowedExitTime = depositor.lockTime + (depositor.unlockTime - depositor.lockTime) * minLockPeriodRatio / _ONE;
    if (block.timestamp < allowedExitTime) revert MinLockPeriodRatioNotReached();

    uint256 amount = depositor.amount;
    if (amount > 0) {
        uint256 balance = balanceOf(msg.sender);
        (uint256 loss, uint256 ret) = _earlyWithdrawLoss(amount, balance);
        if (ret < minReturn) revert MinReturnIsNotMet();
        if (loss > maxLoss) revert MaxLossIsNotMet();
        if (loss > amount * maxLossRatio / _ONE) revert LossIsTooBig();

        _withdraw(depositor, balance); console.log("ret:", ret); console.log("loss:",loss); console.log("feeReceiver: ",feeReceiver);
        oneInch.safeTransfer(to, ret);
        oneInch.safeTransfer(feeReceiver, loss);    
    }
}
```

**Remediation:**  We would recommend to either add a check to redirect the fees to the current contract in case the fee receiver is not set, since a `rescueFunds` function is implemented and those tokens will be able to be withdrawn afterwards. Or to add the `feeReceiver` address as a parameter in the constructor and set the variable so that it is always set.

**Status:** Fixed

- - -